-- ETSI TS 101 376-04-12
-- 11.2.4           Packet cell change order (Iu mode only)
-- Packet Cell Change Order message content

< Packet Cell Change Order message content > ::=
 {
   { 0 < Global TFI : < Global TFI IE > >
   | 10 < G-RNTI : bit (32) > }
   { 0 { < IMMEDIATE_REL : bit >
         < Target Cell: < GMR target cell struct >> }
   | 1 { 00 -- Message escape
         { < IMMEDIATE_REL : bit >
           { 0 | 1 < UTRAN FDD Target cell: < UTRAN FDD Target cell IE > > }
           { 0 | 1 < UTRAN TDD Target cell: < UTRAN TDD Target cell IE > > }
           < padding bits > }
       }
   }
 } ;
-- ! < Non-distribution part error : bit (*) = < no string > > }
-- ! < Message escape : { 01 | 10 | 11} bit (*) = <no string > > } }
-- ! < Address information part error : bit (*) = < no string > > }
-- ! < Distribution part error : bit (*) = < no string > > ;

< GMR target cell struct > ::=
 < ARFCN : bit (11) >
 < BCCH_TN: bit(5) >
 < Spotbeam Id : bit (10) > ;

< 3G Neighbour Cell Description struct > ::=
 { 0 | 1 < Index_Start_3G : bit (7) > }
 { 0 | 1 < Absolute_Index_Start_EMR : bit (7) > }
 { 0 | 1 < UTRAN FDD Description : < UTRAN FDD Description struct >> }
 { 0 | 1 < UTRAN TDD Description : < UTRAN TDD Description struct >> }
 { 0 | 1 < REMOVED_3GCELL_Description : < REMOVED_3GCELL_Description struct >> } ;

< REMOVED_3GCELL_Description struct > ::=
 < N1 : bit (2) >
 { < N2 : bit (5) >
   { < REMOVED_3GCELL_INDEX : bit (7) >
     < 3G_CELL_DIFF_LENGTH : bit (3) >
     < 3GCELL_DIFF : bit (val(3G_CELL_DIFF_LENGTH)) >
   } * (1+val(N2))
 } * (1+val(N1)) ;

< UTRAN FDD Description struct > ::=
 { 0 | 1 < Bandwidth_FDD : bit (3) > }
 { 1 < Repeated UTRAN FDD Neighbour Cells : < Repeated UTRAN FDD Neighbour Cells struct >> } ** 0 ;

< Repeated UTRAN FDD Neighbour Cells struct > ::=
 0 < FDD-ARFCN : bit (14) > -- The value "1" was used in an earlier
 -- version of the protocol and shall not be used.
 < FDD_Indic0 : bit >
 < NR_OF_FDD_CELLS : bit (5) >
 < FDD_CELL_INFORMATION Field : bit(p(NR_OF_FDD_CELLS)) > ;

< UTRAN TDD Description struct > ::=
 { 0 | 1 < Bandwidth_TDD : bit (3) > }
 { 1 < Repeated UTRAN TDD Neighbour Cells : < Repeated UTRAN TDD Neighbour Cells struct >> } ** 0 ;

< Repeated UTRAN TDD Neighbour Cells struct > ::=
 0 < TDD-ARFCN : bit (14) > -- The value "1" was used in an earlier
 -- version of the protocol and shall not be used.
 < TDD_Indic0 : bit >
 < NR_OF_TDD_CELLS : bit (5) >
 < TDD_CELL_INFORMATION Field : bit(q(NR_OF_TDD_CELLS)) > ;

