-- TS 44.060 - d60
-- 11.2.29 Packet Uplink Assignment
-- Packet Uplink Assignment message content

< Packet Uplink Assignment message content > ::=
	< PAGE_MODE : bit (2) >
	{ 0 | 1 <PERSISTENCE_LEVEL : bit (4) > * 4 }
	{	{ 0	< Global TFI : < Global TFI IE > >
		| 10	< TLLI / G-RNTI : bit (32) >
		| 110	< TQI : bit (16) >
		| 111	< Packet Request Reference : < Packet Request Reference IE > > }
		{ 0		-- Message escape
			{	< CHANNEL_CODING_COMMAND : bit (2) >
				< TLLI_BLOCK_CHANNEL_CODING : bit (1) >
				< Packet Timing Advance : < Packet Timing Advance IE > >
				{ 0 | 1	< Frequency Parameters : < Frequency Parameters IE > > }
				{ 01	< Dynamic Allocation : < Dynamic Allocation struct > >
				| 10	< Single Block Allocation : < Single Block Allocation struct > >
				| 00	< extension > 
				}		-- The value '11' was allocated in an earlier version of the protocol and shall not be used.
				{ null | 0 bit** = < no string > -- Receiver backward compatible with earlier version
				| 1						-- Additions for R99
					{ 0 | 1 < Packet Extended Timing Advance : bit (2) > }
					{ null | 0 bit** = <no string>	-- Receiver backward compatible with earlier version
					| 1								-- Additions for Rel-5
						{ 0 | 1 < G-RNTI extension : bit (4) > }
						{ 0 | 1 < RB Id : bit (5) > }					
						{ null | 0 bit** = <no string>	-- Receiver backward compatible with earlier version 
						| 1 								-- Additions for Rel-6
							{ 0 | 1 < PFI : bit (7) > }
							{ 0 | 1 < RLC_MODE : bit (1) > }
							{ null | 0 bit** = < no string > -- Receiver backward compatible with earlier version
							| 1 								-- Additions for Rel-12
								< Primary TSC Set : bit (1) >
						< padding bits > } } } }
				! < Non-distribution part error : bit (*) = < no string > > } 
		| 1					-- Message escape bit used to define EGPRS message contents
			{ 00 {	{ 0 | 1 < CONTENTION_RESOLUTION_TLLI : bit(32) > }
					{ 0 | 1 < COMPACT reduced MA : < COMPACT reduced MA IE >> }
					< EGPRS Channel Coding Command : < EGPRS Modulation and Coding Scheme IE >>
					< RESEGMENT : bit (1) >
					< EGPRS Window Size : < EGPRS Window Size IE > >
					{ 0 | 1 < Access Technologies Request : Access Technologies Request struct >}
					< ARAC RETRANSMISSION REQUEST : bit (1) >
					< TLLI_BLOCK_CHANNEL_CODING : bit (1) >
					{ 0 | 1 < BEP_PERIOD2 : bit (4) > }
					< Packet Timing Advance : < Packet Timing Advance IE > >
					{ 0 | 1 < Packet Extended Timing Advance : bit (2) > }
					{ 0 | 1	< Frequency Parameters : < Frequency Parameters IE > > }
					{	01	< Dynamic Allocation : < Dynamic Allocation struct > >
						| 10	< Multi Block Allocation : < Multi Block Allocation struct > >
						| 00	< extension > 
					}		-- The value '11' was allocated in an earlier version of the protocol and shall not be used.
					{ null | 0 bit** = <no string>				-- Receiver backward compatible with earlier version
					| 1											-- Additions for Rel-5
						{ 0 | 1 < G-RNTI extension : bit (4) > }
						{ 0 | 1 < RB Id : bit (5) > }
						{ null | 0 bit** = <no string>			-- Receiver backward compatible with earlier version 
						| 1 										-- Additions for Rel-6
							{ 0 | 1	< PFI : bit (7) > }
							{ 0 | 1	< RLC_MODE : bit (1) > }
							{ null | 0 bit** = < no string > 	-- Receiver backward compatible with earlier version
							| 1 									-- Additions for Rel-7
								{ 0 | 1	< NPM Transfer Time : bit (5) > }
								{ null | 0 bit** = < no string > -- Receiver backward compatible with earlier version
								| 1 								-- Additions for Rel-9
									< Indication of Upper Layer PDU Start for RLC UM: bit >
									< Enhanced Flexible Timeslot Assignment : bit >
									{ 0 < EMST_NW_Capability : bit (1) >		-- EMST is not used
									| 1 < RLC Entity 2 : < RLC Entity Struct > >	-- EMST is used
										{ 0
										| 1	< RLC Entity 3 : < RLC Entity Struct > > }
									}
									{ null | 0 bit** = < no string > -- Receiver backward compatible with earlier version
									| 1 								-- Additions for Rel-10
										{ 1 < EMSR Additional PFCs 1 : < Additional PFCs struct > > } ** 0	
										{ 1 < EMSR Additional PFCs 2 : < Additional PFCs struct > > } ** 0	
										{ 1 < EMSR Additional PFCs 3 : < Additional PFCs struct > > } ** 0				
										{ null | 0 bit** = < no string > -- Receiver backward compatible with earlier version
										| 1 								-- Additions for Rel-12
											< Primary TSC Set : bit (1) >
					< padding bits > } } } } } }
					! < Non-distribution part error : bit (*) = < no string > > }
			| 01 						-- Message escape for dual carrier, RTTI, BTTI with FANR activated, EGPRS2
				{	{ 0 | 1 < CONTENTION_RESOLUTION_TLLI : bit(32) > }
					< RESEGMENT : bit (1) >
					< Assignment Info : Assignment Info struct >
					< EGPRS Window Size : < EGPRS Window Size IE > >
					{ 0 | 1 < Access Technologies Request : Access Technologies Request struct > }
					< ARAC RETRANSMISSION REQUEST : bit (1) >
					< TLLI_BLOCK_CHANNEL_CODING : bit (1) >
					{ 0 | 1 < BEP_PERIOD2 : bit (4) > }
					< Packet Timing Advance : < Packet Timing Advance IE > >
					{ 0 | 1 < Packet Extended Timing Advance : bit (2) > }
					{ 0						-- BTTI mode
					| 1						-- RTTI mode
						< RTTI_USF_MODE : bit (1) >
						< PDCH Pairs Description : < PDCH Pairs Description IE > > }
					< Dynamic Allocation 2: < Dynamic Allocation 2 struct > > 
					< EGPRS Channel Coding Command : < EGPRS Modulation and Coding Scheme IE >> 
					{ 00		-- No frequency parameters included
					| 01		-- Legacy IEs used
						{ 0 | 1 < Frequency Parameters C1 : < Frequency Parameters IE > > }
						{ 0 | 1 < Frequency Parameters C2 : < Frequency Parameters IE > > }
					| 10		-- Optimized Dual Carrier frequency parameters used 
						 < Dual Carrier Frequency Parameters : < Dual Carrier Frequency Parameters IE > > 
						! < Frequency Parameters error: { 11 } bit(*) = < no string> > -- reserved for future used
					}	
					{ 0 | 1	< PFI : bit (7) > }
					{ 0 | 1	< RLC_MODE : bit (1) > }
					{ 0 | 1	< NPM Transfer Time : bit (5) > }
					{ 0 | 1 	-- '1' indicates that FANR is activated
						{ 0 		-- SSN-based encoding is selected
						| 1 		-- Time-based encoding is selected
							< REPORTED TIMESLOTS C1 : bit (8) >			-- carrier 1 in Downlink Dual Carrier 
																					--configuration
							{ 0 | 1 < REPORTED TIMESLOTS C2 : bit (8) > }	-- carrier 2 in Downlink Dual Carrier 
																					-- configuration
							< TSH : bit (2) > } }
					< Uplink EGPRS Level: < EGPRS Level IE > >
					{ 0 | 1 < Pulse Format: < Pulse Format IE > > }
					{ null | 0 bit** = < no string > -- Receiver backward compatible with earlier version
					| 1 								-- Additions for Rel-9
						< Indication of Upper Layer PDU Start for RLC UM: bit >
						< Enhanced Flexible Timeslot Assignment : bit >
						{ 0 < EMST_NW_Capability : bit (1) >		-- EMST is not used
						| 1 < RLC Entity 2 : < RLC Entity Struct > >	-- EMST is used
							{ 0
							| 1	< RLC Entity 3 : < RLC Entity Struct > > }
						}
						{ 0 | 1 < MTTI_UPLINK_ASSIGNMENT_C1 : bit (1) > }
						{ 0 | 1 < MTTI_UPLINK_ASSIGNMENT_C2 : bit (1) > }
						{ null | 0 bit** = < no string > -- Receiver backward compatible with earlier version
						| 1 								-- Additions for Rel-10
							{ 1 < EMSR Additional PFCs 1 : < Additional PFCs struct > > } ** 0	
							{ 1 < EMSR Additional PFCs 2 : < Additional PFCs struct > > } ** 0	
							{ 1 < EMSR Additional PFCs 3 : < Additional PFCs struct > > } ** 0
							{ null | 0 bit** = < no string > -- Receiver backward compatible with earlier version
							| 1 								-- Additions for Rel-12
								< Primary TSC Set : bit (1) >
					< padding bits > } } }
					! < Non-distribution part error : bit (*) = < no string > > }
			| 10			-- Message escape for downlink multi carrier, RTTI, BTTI with FANR activated, EGPRS2
				{	< RESEGMENT : bit (1) >
					{ 1 < DLMC UL Carrier Info: < DLMC UL Carrier Info struct > > } ** 0
					< EGPRS Window Size : < EGPRS Window Size IE > >
					< UPLINK_TFI_ASSIGNMENT : bit (5) >
					< Packet Timing Advance : < Packet Timing Advance IE > >
					{ 0 | 1 < Packet Extended Timing Advance : bit (2) > }
					{ 0 | 1	< BEP_PERIOD2 : bit (4) > }
					{ 0 | 1	< PFI : bit (7) > }
					{ 0 | 1	< RLC_MODE : bit (1) > }
					{ 0 | 1	< NPM Transfer Time : bit (5) > }
					{ 0 | 1 	-- '1' indicates that FANR is activated
						{ 0 		-- SSN-based encoding is selected
						| 1 		-- Time-based encoding is selected
							< REPORTED TIMESLOTS : bit (8) >	
							< TSH : bit (2) > } }
					< Uplink EGPRS Level: < EGPRS Level IE > >
					{ 0 | 1 < Pulse Format: < Pulse Format IE > > }
					< Indication of Upper Layer PDU Start for RLC UM: bit >
					< Enhanced Flexible Timeslot Assignment : bit >

					< EGPRS Channel Coding Command : < EGPRS Modulation and Coding Scheme IE >> 
					< padding bits > //					-- truncation at end of message allowed, bits '0' assumed
					! < Non-distribution part error : bit (*) = < no string > >
				}
			! < Message escape : { 11 } bit (*) = <no string> >
			}
		} 	-- Extended for future changes
		! < Address information part error : bit (*) = < no string > > } 
	! < Distribution part error : bit (*) = < no string > > ;

<extension> ::=	-- Future extension can be done by modifying this structure
	null ;

<Dynamic Allocation struct > ::= 
	< EXTENDED_DYNAMIC_ALLOCATION : bit (1) >
	{ 0 | 1	< P0 : bit (4) > 
			< PR_MODE : bit (1) > }
	< USF_GRANULARITY : bit (1) >
	{ 0 | 1 < UPLINK_TFI_ASSIGNMENT : bit (5) > }
	0						-- The value '1' was allocated in an earlier version of the protocol and shall not be used.
	{ 0 | 1 < TBF Starting Time : < Starting Frame Number Description IE > > }
	{ 0						-- Timeslot Allocation
		{ 0 | 1 < USF_TN0 : bit (3) > }
		{ 0 | 1 < USF_TN1 : bit (3) > }
		{ 0 | 1 < USF_TN2 : bit (3) > }
		{ 0 | 1 < USF_TN3 : bit (3) > }
		{ 0 | 1 < USF_TN4 : bit (3) > }
		{ 0 | 1 < USF_TN5 : bit (3) > }
		{ 0 | 1 < USF_TN6 : bit (3) > }
		{ 0 | 1 < USF_TN7 : bit (3) > }
	| 1						-- Timeslot Allocation with Power Control Parameters
		< ALPHA : bit (4) >
		{ 0 | 1	< USF_TN0 : bit (3) >
				< GAMMA_TN0 : bit (5) > }
		{ 0 | 1	< USF_TN1 : bit (3) >
				< GAMMA_TN1 : bit (5) > }
		{ 0 | 1	< USF_TN2 : bit (3) >
				< GAMMA_TN2 : bit (5) > }
		{ 0 | 1	< USF_TN3 : bit (3) >
				< GAMMA_TN3 : bit (5) > }
		{ 0 | 1	< USF_TN4 : bit (3) >
				< GAMMA_TN4 : bit (5) > }
		{ 0 | 1	< USF_TN5 : bit (3) >
				< GAMMA_TN5 : bit (5) > }
		{ 0 | 1	< USF_TN6 : bit (3) >
				< GAMMA_TN6 : bit (5) > }
		{ 0 | 1	< USF_TN7 : bit (3) >
				< GAMMA_TN7 : bit (5) > } } ;

<Single Block Allocation struct > ::=
	< TIMESLOT_NUMBER : bit (3) > 
	{ 0 | 1	< ALPHA : bit (4) >
			< GAMMA_TN : bit (5) >}
	{ 0 | 1	< P0 : bit (4) >
			0				-- The value '1' was allocated in an earlier version of the protocol and shall not be used.
			< PR_MODE : bit (1) > }
	< TBF Starting Time : < Starting Frame Number Description IE > > ;

< Multi Block Allocation struct > ::=
	< TIMESLOT_NUMBER : bit (3) >
	{ 0 | 1	< ALPHA : bit (4) >
			< GAMMA_TN : bit (5) >}
	{ 0 | 1	< P0 : bit (4) >
			0				-- The value '1' was allocated in an earlier version of the protocol and shall not be used.
			< PR_MODE : bit (1) > }
	< TBF Starting Time : < Starting Frame Number Description IE > >
	< NUMBER OF RADIO BLOCKS ALLOCATED: bit (2) > ;

< Access Technologies Request struct > ::= -- recursive structure allows any combination of Access technologies
	<Access Technology Type : bit (4) >
	{ 0 | 1 < Access Technologies Request struct > } ;

< Assignment Info struct > ::=
	< Assignment Type : bit (2) >
	< Carrier ID : bit (1) >;

< Dynamic Allocation 2 struct > ::= 
	< EXTENDED_DYNAMIC_ALLOCATION : bit (1) >
	{ 0 | 1	< P0_C1 : bit (4) > 
			< PR_MODE_C1 : bit (1) > 
			{ 0 | 1	< P0_C2 : bit (4) > 
					< PR_MODE_C2 : bit (1) > } }
	< USF_GRANULARITY : bit (1) >
	{ 0 | 1 < UPLINK_TFI_ASSIGNMENT : bit (5) > }
	{ 0			-- Allocation without Power Control Parameters
		< N_USF : bit (4) >
		{ 0 | 1 < USF : bit (3) > } *( val(N_USF) + 1)
	| 1			-- Allocation with Power Control Parameters
		< ALPHA_C1: bit (4) >
		{ 0 | 1 < ALPHA_C2: bit (4) > }
		{ 0							-- BTTI mode
			< N_TS : bit (4) >
			{ 0 | 1 
				< USF : bit (3) >
				< GAMMA: bit (5) > 
			} * ( val(N_TS) + 1)
		| 1							-- RTTI mode
			< N_PAIRS : bit (3) >
			{ 0 | 1 
				< USF : bit (3) > 
				< GAMMA : bit (5) > 
			} * (val(N_PAIRS) + 1) 
			{ 0 		-- RTTI USF
			| 1		-- BTTI USF 
				{ 0 | 1 
					< USF_2 : bit (3) > 
					{ 0 | 1 < GAMMA : bit (5) > }	
				} * (val(N_PAIRS) + 1)
			}
		}
	} ;

< RLC Entity Struct > ::=
	< UPLINK_TFI_ASSIGNMENT : bit (5) >
	< RLC_MODE : bit (1) >
	{ 0 | 1 < EGPRS Window Size : < EGPRS Window Size IE > > }
	< PFI : bit (7) >;

< Additional PFCs struct > ::=
	< UPLINK TFI_ASSIGNMENT : bit (5) >
	{ 0 | 1 < NPM Transfer Time : bit (5) > }
	< PFI : bit (7) >;

< DLMC UL Carrier Info struct > ::= 
	{ 00 					-- Assigned carrier released
		| 01				-- Carrier remains unchanged (no information provided)
		| 10				-- Assigned carrier modified or new carrier assigned
			{ 0						-- BTTI mode
				{ 0	| 1	< UPLINK_TIMESLOT_ALLOCATION : bit (8) > }			-- assigned PDCH
			| 1						-- RTTI mode
				{ 0	| 1 < UPLINK_PDCH_PAIRS_DLMC : bit (8) > }					-- assigned PDCH-pairs
			}
			< Dynamic Allocation 3 : < Dynamic Allocation 3 IE > >
			{ 0		-- EMST is not used on this carrier
				| 1	-- EMST is used on this carrier 
					< Uplink RLC Entity 2 : < RLC Entity Struct > >
					{ 0 | 1 < Uplink RLC Entity 3 : < RLC Entity Struct > > } }
			{ 0 		-- EMSR is not used on this carrier
				| 1	-- EMSR is used on this carrier
					{ 1 < EMSR Additional PFCs 1 : < Additional PFCs struct > > } ** 0	
					{ 1 < EMSR Additional PFCs 2 : < Additional PFCs struct > > } ** 0	
					{ 1 < EMSR Additional PFCs 3 : < Additional PFCs struct > > } ** 0
			}
			< Primary TSC Set : bit (1) >
		| 11 	bit (*) = < no string >			-- reserved
	} ; 

