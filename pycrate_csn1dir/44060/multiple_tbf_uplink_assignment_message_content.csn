-- TS 44.060 - d60
-- 11.2.29a Multiple TBF Uplink Assignment
-- Multiple TBF Uplink Assignment message content

< Multiple TBF Uplink Assignment message content > ::=
	< PAGE_MODE : bit (2) >
	{ 0 | 1 < PERSISTENCE_LEVEL : bit (4) > * 4 }
	{	{ 0	< Global TFI : < Global TFI IE > > 
		| 10	{ < TLLI / G-RNTI : <TLLI / G-RNTI IE > > < G-RNTI extension : bit (4) > } }
		{ 0		-- Message escape bit for GPRS mode TBFs
			{	{ 0 | 1 < CHANNEL_CODING_COMMAND : bit (2) > }
				< TLLI_BLOCK_CHANNEL_CODING : bit (1) >
				< Packet Timing Advance : < Packet Timing Advance IE > >
				{ 0 | 1	< Packet Extended Timing Advance : bit (2) > }
				{ 0 | 1	< Frequency Parameters : < Frequency Parameters IE > > }
				{ 0 | 1	< Uplink TBF Assignment : < Uplink Assignment struct > > } 
				< padding bits >
			} 
			! < Non-distribution part error : bit (*) = < no string > >
		| 1	-- Message escape bit for EGPRS mode TBFs
			{ 00 
				{	{ 0 | 1	< EGPRS Window Size : < EGPRS Window Size IE > > }
					{ 0 | 1	< EGPRS Channel Coding Command : < EGPRS Modulation and Coding Scheme IE > > }
					--< Resegment : < RESEGMENT IE >> -- see case 01 below
					< RESEGMENT : bit (1) >
					< TLLI_BLOCK_CHANNEL_CODING : bit (1) >
					{ 0 | 1	< BEP_PERIOD2 : bit(4) > }
					< Packet Timing Advance : < Packet Timing Advance IE > >	
					{ 0 | 1	< Packet Extended Timing Advance : bit (2) > }
					{ 0 | 1	< Frequency Parameters : < Frequency Parameters IE > > }
					{ 0 | 1	< Uplink TBF Assignment : < Uplink Assignment struct > > }
					{ null | 0 bit** = < no string > -- Receiver backward compatible with earlier version
					| 1 			-- Additions for Rel-7
						{ 0 | 1< NPM Transfer Time : bit (5) > } ** 0
						{ null | 0 bit** = < no string > -- Receiver backward compatible with earlier version
						| 1 								-- Additions for Rel-9
							{ 1 < Indication of Upper Layer PDU Start for RLC UM : bit > } ** 0
							< Enhanced Flexible Timeslot Assignment : bit >
					        < padding bits >
					    }
					}
					! < Non-distribution part error : bit (*) = < no string > >
				}
			| 01 			-- Message escape for dual carrier, RTTI, BTTI with FANR activated, EGPRS2
				{	{ 0 | 1	< EGPRS Window Size : < EGPRS Window Size IE > > }
					{ 0 | 1	< EGPRS Channel Coding Command : < EGPRS Modulation and Coding Scheme IE > > }
					< Assignment Info : < Assignment Info struct > >
					< RESEGMENT : bit (1) >
					< TLLI_BLOCK_CHANNEL_CODING : bit (1) >
					{ 0 | 1 < BEP_PERIOD2 : bit (4) > }
					< Packet Timing Advance : < Packet Timing Advance IE > >
					{ 0 | 1 < Packet Extended Timing Advance : bit (2) > }
					{ 	00		-- No frequency parameters included
						| 01		-- Legacy IEs used
							{ 0 | 1 < Frequency Parameters C1 : < Frequency Parameters IE > > }
							{ 0 | 1 < Frequency Parameters C2 : < Frequency Parameters IE > > }
						| 10		-- Optimized Dual Carrier frequency parameters used 
							< Dual Carrier Frequency Parameters : < Dual Carrier Frequency Parameters IE > > 
						! < Frequency Parameters error: { 11 } bit(*) = < no string> > }		-- reserved for future used
					{ 0 | 1 < Uplink TBF Assignment 2 : < Uplink Assignment 2 struct > > }
					        <Uplink EGPRS Level: < EGPRS Level IE > >
					{ 0 | 1 < Pulse Format: < Pulse Format IE > > }
					{ null | 0 bit** = < no string > -- Receiver backward compatible with earlier version
					| 1 								-- Additions for Rel-9
						{ 1 < Indication of Upper Layer PDU Start for RLC UM : bit > } ** 0
						< Enhanced Flexible Timeslot Assignment : bit >
					    < padding bits >
					}
					! < Non-distribution part error : bit (*) = < no string > >
				}
				! < Message escape : { 10 | 11 } bit (*) = < no string > >
			}
		} -- Extended for future changes
		! < Address information part error : bit (*) = < no string > >
	} 
	! < Distribution part error : bit (*) = < no string > > ;

< Uplink Assignment struct > ::=	
	< EXTENDED_DYNAMIC_ALLOCATION : bit (1) >
	{ 0 | 1	< Uplink Control Timeslot : bit (3) > }
	{ 0 | 1	< P0 : bit (4) > 
			< PR_MODE : bit(1) > }
	{ 0 | 1	< TBF Starting Time : < Starting Frame Number Description IE > > }
	{ 0 | 1	< Global Timeslot description : < Timeslot description struct > >
			{ 1	< Uplink TBF Assignment : < Uplink TBF Assignment struct > > } ** 0 } ;

< Uplink Assignment 2 struct > ::=	
	< EXTENDED_DYNAMIC_ALLOCATION : bit (1) >
	{ 0 | 1	< Uplink Control Timeslot C1 : bit (3) > }
	{ 0 | 1	< Uplink Control Timeslot C2 : bit (3) > }
	{ 0 | 1	< P0_C1 : bit (4) > 
			< PR_MODE_C1 : bit(1) > 
			{ 0 | 1	< P0_C2 : bit (4) > 
					< PR_MODE_C2 : bit(1) > } }
	{ 0 | 1 		-- '1' indicates that FANR is activated
		{ 0 		-- SSN-based encoding is selected
		| 1 		-- Time-based encoding is selected
			< TSH : bit (2) > } }
	{ 0 | 1 
		{ 0 | 1 				-- BTTI mode
			< Global Timeslot description : < Timeslot description 2 struct > >
			{ 1 < Uplink TBF Assignment 2: < Uplink TBF Assignment 2 struct > > } ** 0 
		}
		{ 0 | 1 				-- RTTI mode
			
			< PDCH Pairs Description : < PDCH Pairs Description IE > >
			{ 0					-- without power control parameters 
			| 1					-- with power control parameters 
				< ALPHA_C1 : bit (4) >
				{ 0 | 1 < ALPHA_C2 : bit (4) > }
				< N_PAIRS : bit (3) >
				{ 0 | 1	< GAMMA : bit (5) > } * (val(N_PAIRS) + 1) 
				{ 0 		-- RTTI USF, or no second GAMMA values are given in case of RTTI mode with BTTI USF
				| 1		-- Second GAMMA values are given in case of RTTI mode with BTTI USF 
					{ 0 | 1 < GAMMA : bit (5) > } * (val(N_PAIRS) + 1)
				}
			}
			{ 1 < Uplink TBF Assignment 2: < Uplink TBF Assignment 2 struct > >
				< RTTI_USF_MODE : bit (1) > } ** 0 
		}
	} ;

< Timeslot description struct > ::=
	{ 0														-- without power control params
		< MS_TIMESLOT_ALLOCATION : bit (8) >
	| 1													--	with power control params
		< ALPHA : bit (4) >		
		{ 0 | 1	< GAMMA_TN0 : bit (5) > } 
		{ 0 | 1	< GAMMA_TN1 : bit (5) > }
		{ 0 | 1	< GAMMA_TN2 : bit (5) > }
		{ 0 | 1	< GAMMA_TN3 : bit (5) > }
		{ 0 | 1	< GAMMA_TN4 : bit (5) > }
		{ 0 | 1	< GAMMA_TN5 : bit (5) > }
		{ 0 | 1	< GAMMA_TN6 : bit (5) > }
		{ 0 | 1	< GAMMA_TN7 : bit (5) > } } ;

< Timeslot description 2 struct > ::=
	{ 0														-- without power control params
		< MS_TIMESLOT_ALLOCATION_C1 : bit (8) >
		{ 0 | 1 < MS_TIMESLOT_ALLOCATION_C2 : bit (8) > }
	| 1													--	with power control params
		< ALPHA_C1 : bit (4) >		
		{ 0 | 1	< GAMMA_TN0_C1 : bit (5) > } 
		{ 0 | 1	< GAMMA_TN1_C1 : bit (5) > }
		{ 0 | 1	< GAMMA_TN2_C1 : bit (5) > }
		{ 0 | 1	< GAMMA_TN3_C1 : bit (5) > }
		{ 0 | 1	< GAMMA_TN4_C1 : bit (5) > }
		{ 0 | 1	< GAMMA_TN5_C1 : bit (5) > }
		{ 0 | 1	< GAMMA_TN6_C1 : bit (5) > }
		{ 0 | 1	< GAMMA_TN7_C1 : bit (5) > } 
		{ 0 | 1	< ALPHA_C2 : bit (4) > }		
		{ 0 | 1	< GAMMA_TN0_C2 : bit (5) > } 
		{ 0 | 1	< GAMMA_TN1_C2 : bit (5) > }
		{ 0 | 1	< GAMMA_TN2_C2 : bit (5) > }
		{ 0 | 1	< GAMMA_TN3_C2 : bit (5) > }
		{ 0 | 1	< GAMMA_TN4_C2 : bit (5) > }
		{ 0 | 1	< GAMMA_TN5_C2 : bit (5) > }
		{ 0 | 1	< GAMMA_TN6_C2 : bit (5) > }
		{ 0 | 1	< GAMMA_TN7_C2 : bit (5) > } 
} ;

< Uplink TBF Assignment struct > ::=					-- Recursive for multiple TBFs
	{ 0	< RB Id : bit (5) >
	| 1	< PFI : bit (7) >
		< RLC_MODE : bit (1) > }
	< TFI Assignment : bit (5) >
	{ 0 | 1	< CHANNEL_CODING_COMMAND : bit (2) > }
	{ 0 | 1	< EGPRS Channel Coding Command : < EGPRS Modulation and Coding Scheme IE > > }
	{ 0 | 1	< EGPRS Window Size : < EGPRS Window Size IE > > }
	< USF_GRANULARITY : bit (1) >
	{ 0														-- The timeslots assigned to the TBF are all the timeslots assigned
															-- in the Global Timeslot description
	| 1	< TBF_TIMESLOT_ALLOCATION : bit (N) > }	-- The timeslots assigned to the TBF are a subset of all the
																-- timeslots assigned in the Global Timeslot description. Where
																-- N is the amount of timeslots assigned to the MS in the Global 
																-- Timeslot description
	{ 0		< USF_ALLOCATION : bit (3) >			-- The same USF is valid on all timeslots assigned to the TBF
	| 1														-- Different USF(s) assigned
			< USF_ALLOCATION : bit (3) >					-- USF assignment on the lowest numbered timeslot
																	-- assigned to the TBF
			{ 0 | 1 < USF_ALLOCATION : bit (3) > } * (M-1) } ;	-- USFs on subsequent timeslots assigned to the TBF:

< Uplink TBF Assignment 2 struct > ::=					-- Recursive for multiple TBFs
	< PFI : bit (7) >
	< RLC_MODE : bit (1) > 
	< TFI Assignment : bit (5) >
	{ 0 | 1	< EGPRS Channel Coding Command: < EGPRS Modulation and Coding Scheme IE > > }
	{ 0 | 1	< EGPRS Window Size : < EGPRS Window Size IE > > }
	< USF_GRANULARITY : bit (1) >
	{ 0 | 1	< NPM Transfer Time : bit (5) > }
	{ 0 | 1 	-- '1' indicates that time-based FANR is selected
			< REPORTED TIMESLOTS C1: bit (8) >					-- carrier 1 in Downlink Dual Carrier configuration
			{ 0 | 1 < REPORTED TIMESLOTS C2 : bit (8) > }			-- carrier 2 in Downlink Dual Carrier configuration
	} 
	{ 0										-- The timeslots/PDCH-pairs assigned to the TBF are all the timeslots assigned
											-- in the Global Timeslot description or PDCH pair description
	| 1	< TBF_TIMESLOT_ALLOCATION : bit (N) > }	-- see description in Table 11.2.29a.2
	{ 0	< USF_ALLOCATION_C1 : bit (3) >
		{ 0 | 1 < USF_ALLOCATION_C2 : bit (3) > } 	-- The same USF is valid on all timeslots/PDCH-pairs assigned 																-- to the TBF for each specified carrier
		< USF_ALLOCATION : bit (3) >
		{ 0 | 1 < USF_ALLOCATION : bit (3) > } * (M-1) 
	} ;	

< Assignment Info struct > ::=
	< Assignment Type : bit (2) >
	< Carrier ID : bit (1) > ;

