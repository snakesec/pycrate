-- TS 44.060 - d60
-- 11.2.43 PS Handover Command
-- PS Handover Command message content

< PS Handover Command message content > ::=
	< PAGE_MODE : bit (2) >
	{	0 < Global TFI : < Global TFI IE > >
		{	-- Non-distribution part
			< CONTAINER_ID : bit(2) >
			{ 00	< PS Handover to A/Gb Mode Payload :
					  { 00 < PS Handover RR Info: < PS Handover Radio Resources IE > >
					  | 01 < PS Handover RR 2 Info: < PS Handover Radio Resources 2 IE > >
					  | 10 < PS Handover RR 3 Info: < PS Handover Radio Resources 3 IE > >
					  ! < RR Handover RR Info Error : { 11 } > < bit (*) = <no string> > } > -- Extended for future changes
					{ 0 | 1 < NAS Container for PS Handover IE > }
			| 01	< PS Handover to UTRAN Payload :
					  < RRC Container IE > >
			| 10	< PS Handover to E-UTRAN Payload :
					  < RRC Container IE > >
			! < Message escape : 11 > < bit (*) = < no string > > }		-- Extended for future changes 
			{ null | 0	< bit ** = < no string > >	-- Receiver compatible with earlier release
			| 1		-- Additions in Rel-8 :
				{ 0 | 1	< E-UTRAN_CCN_ACTIVE : bit (1) > } 
				{ 0 | 1	< Measurement Control Parameters Description : 
						< Measurement Control Parameters Description >>}
				{ null | 0 < bit** = < no string > > -- Receiver backward compatible with earlier version
				| 1 							     -- Additions for Rel-9
					< Enhanced Flexible Timeslot Assignment : bit >
						{ 0 < EMST_NW_Capability : bit (1) >		-- EMST is not used
						| 1											-- EMST is used
							{ 0
							| 1	< Downlink RLC Entity 2 : < RLC Entity Struct > >
								{ 0
								| 1 < Downlink RLC Entity 3 : < RLC Entity Struct > > }
							}
							{ 0
							| 1	< Uplink RLC Entity 2 : < RLC Entity Struct > >
								{ 0
								| 1 < Uplink RLC Entity 3 : < RLC Entity Struct > > }
							}
						}
						{ null | 0 < bit** = < no string > > -- Receiver backward compatible with earlier version
						| 1 							     -- Additions for Rel-10
							{ 0		-- EMSR is not enabled in downlink
							| 1		-- EMSR is enabled in downlink
								{ 1 < EMSR Additional PFCs 1 : < Additional PFCs struct > > } ** 0	
								{ 1 < EMSR Additional PFCs 2 : < Additional PFCs struct > > } ** 0	
								{ 1 < EMSR Additional PFCs 3 : < Additional PFCs struct > > } ** 0
							}
							{ 0		-- EMSR is not enabled in uplink
							| 1		-- EMSR is enabled in uplink
								{ 1 < EMSR Additional PFCs 1 : < Additional PFCs struct > > } ** 0	
								{ 1 < EMSR Additional PFCs 2 : < Additional PFCs struct > > } ** 0	
								{ 1 < EMSR Additional PFCs 3 : < Additional PFCs struct > > } ** 0
							}
 		{ null | 0 < bit** = < no string > > -- Receiver backward compatible with earlier version
							| 1 		     -- Additions for Rel-11
 		{ 0 | 1 < Selected PLMN Index : bit(3) >}
								{ null | 0 < bit** = < no string > > -- Receiver backward compatible with earlier version
								| 1 							     -- Additions for Rel-12
									< Primary TSC Set : bit (1) >
									{ 0 | 1						-- Secondary DL TSC Value assigned
										< Secondary DL TSC Set : bit (1) >
										< Secondary DL TSC Value : bit (3) > }
			< padding bits > } } } } }
			! < Non-distribution part error : bit (*) = < no string > > }
		! < Address information part error : bit (*) = < no string > > }
	! < Distribution part error : bit (*) = < no string > > ;

< RLC Entity Struct > ::=
	< TFI : bit (5) >
	< RLC_MODE : bit (1) >
	{ 0 | 1 < EGPRS Window Size : < EGPRS Window Size IE > > }
	< PFI : bit (7) >;

< Additional PFCs struct > ::=
	< TFI : bit (5) >
	{ 0 | 1 < NPM Transfer Time : bit (5) > }
	< PFI : bit (7) >;

