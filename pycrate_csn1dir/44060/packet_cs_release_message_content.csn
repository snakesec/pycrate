-- TS 44.060 - d60
-- 11.2.37 Packet CS Release Indication
-- Packet CS Release message content

< Packet CS Release message content > ::=
	< PAGE_MODE : bit (2) >
	{	< GLOBAL_TFI : < Global TFI IE > >
		< ENHANCED_DTM_CS_RELEASE_INDICATION : bit >
		< Global Packet Timing Advance : < Global Packet Timing Advance IE > >
		{ 00		-- RR connection is released and the MS maintains its DL and/or UL TBF(s)
			< padding bits >		-- Allows future message extension for '00' choice.
		| 01		-- When RR connection is released, PDTCH/H is converted to PDTCH/F 
					-- and the MS maintains its DL and/or UL TBF(s)
			< padding bits >		-- Allows future message extension for '01' choice.
		| 10		-- RR connection is released and DL and/or UL TBF(s) are reconfigured
			{ 0		-- Message escape for GPRS mode TBFs
				{	{ 0 | 1	< Frequency Parameters : < Frequency Parameters IE > > }
					{ 0 | 1	< Packet Extended Timing Advance : bit (2) > }
					{ 0 | 1 									-- downlink TBF(s)
						{ 1	< Multiple Downlink Assignment : < Multiple Downlink Assignment struct > > } ** 0 
					}
					{ 0 | 1 									-- uplink TBF(s)
						{ 0 | 1	< CHANNEL_CODING_COMMAND : bit (2) > }
						< Multiple Uplink Assignment : < Multiple Uplink Assignment struct > > 
				}
				{ null | 0 bit** = < no string > -- Receiver backward compatible with earlier version
				| 1 								-- Additions for Rel-12
					< Primary TSC Set : bit (1) >
					{ 0 | 1						-- Secondary DL TSC Value assigned
						< Secondary DL TSC Set : bit (1) >
						< Secondary DL TSC Value : bit (3) > }
				< padding bits > }
				! < Non-distribution part error : bit (*) = < no string > > }
			| 1				-- Message escape bit for EGPRS mode TBFs
				{ 00 {	
						{ 0 | 1	< Frequency Parameters : < Frequency Parameters IE > > }
						{ 0 | 1	< Packet Extended Timing Advance : bit (2) > }
						{ 0 | 1	< BEP_PERIOD2 : bit(4) > } 
						{ 0 | 1 								-- downlink TBF(s)
							{ 0 | 1 < Downlink EGPRS Window Size : < EGPRS Window Size IE > > }
							< LINK_QUALITY_MEASUREMENT_MODE : bit (2) >
							{ 1	< Multiple Downlink Assignment : 
								< Multiple Downlink Assignment struct > > } ** 0
						}
						{ 0 | 1								-- uplink TBF(s)
							{ 0 | 1	< EGPRS Channel Coding Command : 
									< EGPRS Modulation and Coding Scheme IE >> }
							< RESEGMENT : bit (1) >
							{ 0 | 1 < Uplink EGPRS Window Size : < EGPRS Window Size IE > > }
							< Multiple Uplink Assignment : < Multiple Uplink Assignment struct > > 
						}
						{ null | 0 bit** = < no string > -- Receiver backward compatible with earlier version
						| 1 			-- Additions for Rel-7
							{ 1	{ 0 | 1< NPM Transfer Time : bit (5) > } }** 0
							{ null | 0 bit** = < no string > -- Receiver backward compatible with earlier version
							| 1 								-- Additions for Rel-9
								< Enhanced Flexible Timeslot Assignment : bit >
								{ 0		-- EMST is not used in DL
								| 1		-- EMST is used in DL
									< Downlink RLC Entity 2 : < RLC Entity struct > >
									{ 0
									| 1 < Downlink RLC Entity 3 : < RLC Entity struct > > }
								}
								{ 0		-- EMST is not used in UL
								| 1		-- EMST is used in UL
									< Uplink RLC Entity 2 : < RLC Entity struct > >
									{ 0
									| 1 < Uplink RLC Entity 3 : < RLC Entity struct > > }
								}
								{ null | 0 bit** = < no string > -- Receiver backward compatible with earlier version
								| 1 								-- Additions for Rel-10
									{ 0		-- EMSR is not enabled in downlink
									| 1		-- EMSR is enabled in downlink
										{ 1 < EMSR Additional PFCs 1 : < Additional PFCs struct > > } ** 0	
										{ 1 < EMSR Additional PFCs 2 : < Additional PFCs struct > > } ** 0	
										{ 1 < EMSR Additional PFCs 3 : < Additional PFCs struct > > } ** 0
									}
									{ 0		-- EMSR is not enabled in uplink
									| 1		-- EMSR is enabled in uplink
										{ 1 < EMSR Additional PFCs 1 : < Additional PFCs struct > > } ** 0	
										{ 1 < EMSR Additional PFCs 2 : < Additional PFCs struct > > } ** 0	
										{ 1 < EMSR Additional PFCs 3 : < Additional PFCs struct > > } ** 0
									}
									{ null | 0 bit** = < no string > -- Receiver backward compatible with earlier version
									| 1 								-- Additions for Rel-12
										< Primary TSC Set : bit (1) >
										{ 0 | 1						-- Secondary DL TSC Value assigned
											< Secondary DL TSC Set : bit (1) >
											< Secondary DL TSC Value : bit (3) > }
									< padding bits > } } } }
						! < Non-distribution part error : bit (*) = < no string > > 
					}
				| 01	-- Message escape for Dual Carrier, BTTI with FANR activated, RTTI, EGPRS2
					{
						< Assignment Info : Assignment Info struct >
						{ 00		-- No frequency parameters included
						| 01		-- Legacy IEs used
							< Frequency Parameters C1: < Frequency Parameters IE > > 
							{ 0 | 1 < Frequency Parameters C2: < Frequency Parameters IE > > }
						| 10		-- Optimized Dual Carrier frequency parameters used
							< Dual Carrier Frequency Parameters: < Dual Carrier Frequency Parameters IE > > 
						! < Frequency parameters error: { 11 } bit (*) = <no string> > -- Reserved for future use
						}
						{ 0 | 1	< Packet Extended Timing Advance : bit (2) > }
						< EGPRS mode : < EGPRS mode 2 IE > >

						{ null | 0 bit** = < no string > -- Receiver backward compatible with earlier version
						| 1 								-- Additions for Rel-9
							< Enhanced Flexible Timeslot Assignment : bit >
							{ 0		-- EMST is not used in DL
							| 1	< Downlink RLC Entity 2 : < RLC Entity struct > >	-- EMST is used in DL
								{ 0
								| 1 < Downlink RLC Entity 3 : < RLC Entity struct > > }
							}
							{ 0		-- EMST is not used in UL
							| 1	< Uplink RLC Entity 2 : < RLC Entity struct > >	-- EMST is used in UL
								{ 0
								| 1 < Uplink RLC Entity 3 : < RLC Entity struct > > }
							}
							{ null | 0 bit** = < no string > -- Receiver backward compatible with earlier version
							| 1 								-- Additions for Rel-10
								{ 0		-- EMSR is not enabled in downlink
								| 1		-- EMSR is enabled in downlink
									{ 1 < EMSR Additional PFCs 1 : < Additional PFCs struct > > } ** 0	
									{ 1 < EMSR Additional PFCs 2 : < Additional PFCs struct > > } ** 0	
									{ 1 < EMSR Additional PFCs 3 : < Additional PFCs struct > > } ** 0
								}
								{ 0		-- EMSR is not enabled in uplink
								| 1		-- EMSR is enabled in uplink
									{ 1 < EMSR Additional PFCs 1 : < Additional PFCs struct > > } ** 0	
									{ 1 < EMSR Additional PFCs 2 : < Additional PFCs struct > > } ** 0	
									{ 1 < EMSR Additional PFCs 3 : < Additional PFCs struct > > } ** 0
								}
							{ null | 0 bit** = < no string > -- Receiver backward compatible with earlier version
							| 1 								-- Additions for Rel-11
								< EGPRS Packet Downlink Ack/Nack Type 3 Support : bit(1) >
								{ null | 0 bit** = < no string > -- Receiver backward compatible with earlier version
								| 1 								-- Additions for Rel-12
									< Primary TSC Set : bit (1) >
									{ 0 | 1						-- Secondary DL TSC Value assigned
										< Secondary DL TSC Set : bit (1) >
										< Secondary DL TSC Value : bit (3) > }
								< padding bits > } } } }
					! < Non-distribution part error : bit (*) = < no string > > }
				! < Message escape : { 10 | 11} bit (*) = < no string > > } -- Extended for future changes
			}
		| 11		-- Reserved for future use. When received it shall be interpreted as "00".
			< padding bits >		-- Allows future message extension for '11' choice.
		}
		! < Address information part error : bit (*) = < no string > > }
	! < Distribution part error : bit (*) = < no string > > ;

< Multiple Downlink Assignment struct > ::=
	< TIMESLOT_ALLOCATION : bit (8) >
	{ 0 | 1 	< Uplink Control Timeslot : bit (3) > }
	{ 1 < Downlink TBF assignment : < Downlink TBF assignment struct > > } ** 0 ;

< Downlink TBF assignment struct > ::=
	{ 0 | 1	< PFI : bit (7) > }
	< DOWNLINK_RLC_MODE : bit (1) >
	{ 0 | 1	< Uplink Control Timeslot : bit (3) > }
	< TFI_ASSIGNMENT : bit (5) >
	< CONTROL_ACK : bit (1) >
	{ 0 | 1	< Downlink EGPRS Window Size : < EGPRS Window Size IE > > } ;

< Multiple Uplink Assignment struct > ::=
	< EXTENDED_DYNAMIC_ALLOCATION : bit (1) >
	{ 0 | 1	< P0 : bit (4) > 
			< PR_MODE : bit(1) > }
	{ 0 | 1	< Global Timeslot description : < Timeslot description struct > >
			{ 1	< Uplink TBF Assignment : < Uplink TBF Assignment struct > > } ** 0 } ;

< Timeslot description struct > ::=
	{ 0													-- without power control params
		< MS_TIMESLOT_ALLOCATION : bit (8) >
	| 1													--	with power control params
		< ALPHA : bit (4) >		
		{ 0 | 1	< GAMMA_TN0 : bit (5) > } 
		{ 0 | 1	< GAMMA_TN1 : bit (5) > }
		{ 0 | 1	< GAMMA_TN2 : bit (5) > }
		{ 0 | 1	< GAMMA_TN3 : bit (5) > }
		{ 0 | 1	< GAMMA_TN4 : bit (5) > }
		{ 0 | 1	< GAMMA_TN5 : bit (5) > }
		{ 0 | 1	< GAMMA_TN6 : bit (5) > }
		{ 0 | 1	< GAMMA_TN7 : bit (5) > } } ;

< Uplink TBF Assignment struct > ::=					-- Recursive for multiple TBFs
	{ 0 | 1	< PFI : bit (7) > }
	< TFI_ASSIGNMENT : bit (5) >
	{ 0 | 1	< CHANNEL_CODING_COMMAND : bit (2) > }
	{ 0 | 1	< EGPRS Channel Coding Command : < EGPRS Modulation and Coding Scheme IE > > }
	{ 0 | 1	< Uplink EGPRS Window Size : < EGPRS Window Size IE > > }
	< USF_GRANULARITY : bit (1) >
	{ 0														-- The timeslots assigned to the TBF are all the timeslots assigned
															-- in the Global Timeslot description
	| 1	< TBF_TIMESLOT_ALLOCATION : bit (N) > }	-- The timeslots assigned to the TBF are a subset of all the
																-- timeslots assigned in the Global Timeslot description. Where
																-- N is the amount of timeslots assigned to the MS in the Global 
																-- Timeslot description
	{ 0		< USF_ALLOCATION : bit (3) >			-- The same USF is valid on all timeslots assigned to the TBF
	| 1		< USF_ALLOCATION : bit (3) >			-- Different USF(s) assigned
															-- USF assignment on the lowest numbered timeslot
															-- assigned to the TBF
			{ 0 | 1 < USF_ALLOCATION : bit (3) > } * (M-1) } ;	-- USFs on subsequent timeslots assigned to the TBF:

< Assignment Info struct > ::=
	< Assignment Type : bit (2) >
	< Carrier ID : bit (1) > ;

< RLC Entity struct > ::=
	< TFI_ASSIGNMENT : bit (5) >
	< RLC_MODE : bit (1) >
	{ 0 | 1 < EGPRS Window Size : < EGPRS Window Size IE > > }
	< PFI : bit (7) >;

< Additional PFCs struct > ::=
	< TFI_ASSIGNMENT : bit (5) >
	{ 0 | 1 < NPM Transfer Time : bit (5) > }
	< PFI : bit (7) >;

