-- TS 44.060 - d60
-- 11.2.9b Packet Measurement Order
-- Packet Measurement Order message content

< Packet Measurement Order message content > ::=
	< PAGE_MODE : bit (2) >
	{	{ 0	< Global TFI : < Global TFI IE > >
		| 10	< TLLI / G-RNTI : bit (32) > }
		{ < PMO_INDEX : bit (3) >
		< PMO_COUNT : bit (3) >
		{ 0 | 1 < NC Measurement Parameters : < NC Measurement Parameters struct > > }
		0 						-- The value '1' was allocated in an earlier version of the protocol and shall not be used. 
			{ null 	| 0 bit** = < no string > 				-- Receiver compatible with earlier release
					| 1										-- Additions in release 98 :
				{ 0 | 1 < LSA Parameters : < LSA Parameters IE >> }
				{ null	| 0 bit** = < no string >			-- Receiver compatible with earlier release
						| 1 									-- Additions in release 99 :
					{ 0	| 1 < ENH Measurement Parameters : < ENH Measurement Parameters struct >> }
					{ null	| 0 bit** = < no string >		-- Receiver compatible with earlier release
							| 1 								-- Additions in Rel-4 :
						< CCN_ACTIVE : bit >
						{ 0 | 1 < CCN Support Description : < CCN Support Description struct >> }
						{ null	| 0	bit ** = < no string >	-- Receiver compatible with earlier release
								| 1							-- Additions in Rel-5 :
							{ 0 | 1	< G-RNTI extension : bit (4) > }
							{ 0 | 1	< Iu Mode Neighbour Cell Parameters : 
									{ 1 < Iu Mode Neighbour Cell params struct > } ** 0 > }
										-- Supplementary information for dual Iu mode and A/Gb mode capable cells
							{ 0 | 1	< NC Iu MODE ONLY CAPABLE CELL LIST : NC Iu Mode Only Cell List struct > }
							{ 0 | 1	< GPRS 3G Additional Measurement Parameters Description 2 : 
									< GPRS 3G Additional Measurement Parameters Description 2 struct >> }
							{ null	| 0 bit** = < no string >		-- Receiver compatible with earlier release
							| 1 										-- Additions in Rel-6 :
								< 3G_CCN_ACTIVE : bit >
								{ null	| 0	bit ** = < no string >	-- Receiver compatible with earlier release
								| 1									-- Additions in Rel-7 :						
									{ 0 | 1	< 700_REPORTING_OFFSET : bit (3) > 
											< 700_REPORTING_THRESHOLD : bit (3) > }
									{ 0 | 1	< 810_REPORTING_OFFSET : bit (3) > 
											< 810_REPORTING_THRESHOLD : bit (3) > }
									{ null	| 0 bit** = 	< no string >	-- Receiver compatible with earlier release
									| 1 							-- Additions in Rel-8
										{ 0 | 1	< 3G_BA_IND : bit > < PMO_IND : bit > }
										{ 0 | 1	< Priority and E-UTRAN Parameters Description : 
												< Priority and E-UTRAN Parameters Description struct >> }
										{ 0 | 1	< Individual Priorities : < Individual Priorities IE >> }
										{ 0 | 1	< 3G CSG Description : < 3G CSG Description IE >> }
										{ 0 | 1	< E-UTRAN CSG Description : < E-UTRAN CSG Description IE >> }
										{ 0 | 1	< Measurement Control Parameters Description :
												< Measurement Control Parameters Description >> }
										{ null	| 0 bit** = 	< no string >	-- Receiver compatible with earlier release
										| 1 									-- Additions in Rel-9
											{ 0 | 1	< Enhanced Cell Reselection Parameters Description :
													< Enhanced Cell Reselection Parameters IE >> }
											{ 0 | 1	< CSG Cells Reporting Description : 
													< CSG Cells Reporting Description struct >> }
											{ null	| 0 bit** = 	< no string >	-- Receiver compatible with earlier release
											| 1 									-- Additions in Rel-11
												{ 0 | 1	< E-UTRAN IPP with extended EARFCNs: 
														< E-UTRAN IPP with extended EARFCNs IE>> }
												{ 0 | 1	< E-UTRAN NC with extended EARFCNs: 
														< E-UTRAN NC with extended EARFCNs IE>> }

										< padding bits > } } } } } } } } }
			! < Non-distribution part error : bit (*) = < no string > > }
		! < Address information part error : bit (*) = < no string > > }
	! < Distribution part error : bit (*) = < no string > > ;

< NC Measurement Parameters struct > ::=
	< NETWORK_CONTROL_ORDER : bit (2) >
	{ 0 | 1	< NC_ NON_DRX_PERIOD : bit (3) >
			< NC_REPORTING_PERIOD_I : bit (3) >
			< NC_REPORTING_PERIOD_T : bit (3) > }
	{ 0 | 1	< NC_FREQUENCY_LIST : < NC Frequency list struct > > } ;

< NC Frequency list struct > ::=
	{ 0 | 1	{ < NR_OF_REMOVED_FREQ : bit (5) >
			{ < REMOVED_FREQ_INDEX : bit (6) > } * (1 + val(NR_OF_REMOVED_FREQ)) } }
	{ 1 < List of added Frequency struct : < Add Frequency list struct > >} ** 0;

< Add Frequency list struct > ::=
	< START_FREQUENCY : bit (10) >
	< BSIC : bit (6) >
	{ 0 | 1	< Cell selection params : < Cell Selection struct > > }
	< NR_OF_FREQUENCIES : bit (5) >
	< FREQ_DIFF_LENGTH : bit (3) >
	{	< FREQUENCY_DIFF : bit (1+val(FREQ_DIFF_LENGTH)) >
		< BSIC : bit (6) >
		{ 0 | 1	< Cell selection params : < Cell Selection struct > > } } * (val(NR_OF_FREQUENCIES));

< Cell Selection struct > ::=
	< CELL_BAR_ACCESS_2 : bit (1) >
	< EXC_ACC : bit >
	< SAME_RA_AS_SERVING_CELL : bit (1) >
	{ 0 | 1	< GPRS_RXLEV_ACCESS_MIN : bit (6) >
			< GPRS_MS_TXPWR_MAX_CCH : bit (5) > }
	{ 0 | 1	< GPRS_TEMPORARY_OFFSET : bit (3) >
			< GPRS_PENALTY_TIME : bit (5) > }
	{ 0 | 1	< GPRS_RESELECT_OFFSET : bit (5) > }
	{ 0 | 1	< HCS params : < HCS struct > > }
	{ 0 | 1	< SI13_PBCCH_LOCATION : < SI13_PBCCH_LOCATION struct > > } ;

< SI13_PBCCH_LOCATION struct > ::=
	{ 0	< SI13_LOCATION : bit (1) >
	| 1	< PBCCH_LOCATION : bit (2) >
		< PSI1_REPEAT_PERIOD : bit (4) > } ;

< HCS struct > ::=
	< PRIORITY_CLASS : bit (3) >
	< HCS_THR : bit (5) > ;

< ENH Measurement parameters struct > ::= 
	{ 0 < BA_IND : bit > < 3G_BA_IND : bit > | 1 < PSI3_CHANGE_MARK : bit(2) > }
	< PMO_IND : bit >
	< REPORT_TYPE : bit >
	< REPORTING_RATE : bit >
	< INVALID_BSIC_REPORTING : bit >
	{ 0 | 1	< 3G Neighbour Cell Description : < 3G Neighbour Cell Description struct >> }
	{ 0 | 1	< GPRS REP PRIORITY Description : <GPRS REP PRIORITY Description struct >> }
	{ 0 | 1	< GPRS MEASUREMENT Parameters Description : 
			< GPRS MEASUREMENT PARAMETERS Description struct >> }
	{ 0 | 1	< GPRS 3G MEASUREMENT Parameters Description : 
			< GPRS 3G MEASUREMENT PARAMETERS BIS Description struct >> } ;

< 3G Neighbour Cell Description struct > ::=
	{ 0 | 1 < Index_Start_3G : bit (7)> }
	{ 0 | 1 < Absolute_Index_Start_EMR : bit (7)> }
	{ 0 | 1 < UTRAN FDD Description : < UTRAN FDD Description struct > > }
	{ 0 | 1 < UTRAN TDD Description : < UTRAN TDD Description struct > > }
	{ 0 | 1 < CDMA2000 Description : < CDMA 2000 Description struct > > }
	{ 0 | 1 < REMOVED_3GCELL_Description : < REMOVED_3GCELL_Description struct >> } ;

< REMOVED_3GCELL_Description struct > ::=
	< N1 : bit (2) >
	{	< N2 : bit (5) >
		{	< REMOVED_3GCELL_INDEX : bit (7) >
			< 3G_CELL_DIFF_LENGTH : bit (3) >
			< 3GCELL_DIFF : bit (val(3G_CELL_DIFF_LENGTH)) >
		} * (1+val(N2))
	} * (1+val(N1)) ;

< UTRAN FDD Description struct > ::=
	{ 0 | 1 < Bandwidth_FDD : bit (3) > }
	{ 1 < Repeated UTRAN FDD Neighbour Cells : < Repeated UTRAN FDD Neighbour Cells struct >> } ** 0 ;

< Repeated UTRAN FDD Neighbour Cells struct > ::=
	0 < FDD-ARFCN : bit (14) >		-- The value ‘1’ was used in an earlier 
										-- version of the protocol and shall not be used.
	< FDD_Indic0 : bit >
	< NR_OF_FDD_CELLS : bit (5) >
	< FDD_CELL_INFORMATION Field : bit(p(NR_OF_FDD_CELLS)) > ; 

< UTRAN TDD Description struct > ::=
	{ 0 | 1 < Bandwidth_TDD : bit (3) > }
	{ 1 < Repeated UTRAN TDD Neighbour Cells : < Repeated UTRAN TDD Neighbour Cells struct >> } ** 0 ;

< Repeated UTRAN TDD Neighbour Cells struct > ::=
	< TDD_Indic0 : bit >
	0 < TDD-ARFCN : bit (14) >		-- The value ‘1’ was used in an earlier
										-- version of the protocol and shall not be used.
	< NR_OF_TDD_CELLS : bit (5) >
	< TDD_CELL_INFORMATION Field : bit(q(NR_OF_TDD_CELLS)) > ; 

< CDMA 2000 Description struct > ::=
	< cdma2000 frequency band : bit (5) >
	< cdma2000 frequency : bit (11) >
	< number_cdma2000_cells : bit (5) >
	{ < Pilot PN offset : bit (9) >
		-- this information is enough for 1X Common Pilot
	{0 | 1{ 000 { <TD_MODE : bit (2) > <TD_POWER_LEVEL : bit (3) >}
		-- additional information for 1X Common Pilot with Transmit Diversity
		| 001 { < QOF : bit (2) > <WALSH_LEN_A : bit (3) > 
			< AUX_PILOT_WALSH : bit(val(WALSH_LEN_A)+6)>}
			-- additional information for 1X Auxiliary Pilot
		| 010 { < QOF : bit (2) > <WALSH_LEN_B : bit (3) > 
				< AUX_TD_WALSH : bit(val(WALSH_LEN_B)+6)>
				< AUX_TD_POWER_LEVEL : bit (2) > <TD_MODE : bit (2) >}
				-- additional information for 1X Auxiliary Pilot with Transmit Diversity
		| 011 { < SR3_PRIM_PILOT : bit (2) > <SR3_PILOT_POWER1 : bit (3) >
				< SR3_PILOT_POWER2 : bit (3) >}
				-- additional information for 3X Common Pilot
		| 110 { < SR3_PRIM_PILOT : bit (2) > <SR3_PILOT_POWER1 : bit (3) >
				< SR3_PILOT_POWER2 : bit (3) > <QOF : bit (2) >
				< WALSH_LEN_C : bit (3) > 
				< AUX_WALSH_LEN : bit(val(WALSH_LEN_C)+6)>
			{ 0 | 1 < QOF1 : bit (2) > < WALSH_LENGTH1 : bit (3) > 
						< AUX_PILOT_WALSH1 : bit(val(WALSH_LENGTH1)+6)>}
			{ 0 | 1 < QOF2 : bit (2) > <WALSH_LENGTH2 : bit (3) >
						<AUX_PILOT_WALSH2 : bit(val(WALSH_LENGTH2)+6)>}}
						-- additional information for 3X Auxiliary Pilot
			}
		}
	} * (val(number_cdma2000_cells)) ;

< GPRS REP PRIORITY Description struct > ::=
	< Number_Cells : bit(7) >
	{ < REP_PRIORITY : bit >} * (val(Number_Cells)) ;

< GPRS MEASUREMENT PARAMETERS Description struct > ::=
	{ 0 | 1	< MULTIBAND_REPORTING : bit (2) > }
	{ 0 | 1	< SERVING_BAND_REPORTING : bit (2) > }
	< SCALE_ORD : bit(2) >

	{ 0 | 1	< 900_REPORTING_OFFSET : bit (3) > 
			< 900_REPORTING_THRESHOLD : bit (3) > }

	{ 0 | 1	< 1800_REPORTING_OFFSET : bit (3) > 
			< 1800_REPORTING_THRESHOLD : bit (3) > }

	{ 0 | 1	< 400_REPORTING_OFFSET : bit (3) > 
			< 400_REPORTING_THRESHOLD : bit (3) > }

	{ 0 | 1	< 1900_REPORTING_OFFSET : bit (3) > 
			< 1900_REPORTING_THRESHOLD : bit (3) > }

	{ 0 | 1	< 850_REPORTING_OFFSET : bit (3) > 
			< 850_REPORTING_THRESHOLD : bit (3) > } ;

< GPRS 3G MEASUREMENT PARAMETERS BIS Description struct > ::=
	< Qsearch_P : bit (4) > 
	{ 1 ! < Ignore : bit = < no string >> }			-- this bit shall be ignored by the receiver
													-- for backward compatibility with earlier releases
	{ 0 | 1	< FDD_REP_QUANT : bit >													-- FDD Parameters
			< FDD_MULTIRAT_REPORTING : bit (2) > }
	
	{ 0 | 1	< FDD_REPORTING_OFFSET : bit (3) > 
			< FDD_REPORTING_THRESHOLD : bit (3) > }

	{ 0 | 1	< TDD_MULTIRAT_REPORTING : bit (2) > }								-- TDD Parameters
	{ 0 | 1	< TDD_REPORTING_OFFSET : bit (3) > 
			< TDD_REPORTING_THRESHOLD : bit (3) > }

	{ 0 | 1	< CDMA2000_MULTIRAT_REPORTING : bit (2) > }						-- CDMA2000 Parameters
	{ 0 | 1	< CDMA2000_REPORTING_OFFSET : bit (3) > 
			< CDMA2000_REPORTING_THRESHOLD : bit (3) > } ;

< CCN Support Description struct > ::=
    < Number_Cells : bit (7) >
    { < CCN_SUPPORTED : bit > } * (val(Number_Cells)) ;

< Iu Mode Neighbour Cell Params struct > ::=
	{ 0 | 1 < Iu Mode Cell Selection Params : < Iu Mode Cell Selection struct > > }
			< NR_OF_FREQUENCIES : bit (5) >
			{ 0 | 1	< Iu Mode Cell Selection Params : 
					< Iu Mode Cell Selection struct > > } * (val(NR_OF_FREQUENCIES)) ;

< Iu Mode Cell Selection struct > ::=
	< CELL BAR QUALIFY 3 : bit (2) >	
	{ 0 | 1	< SI13Alt PBCCH Location: < SI13 PBCCH Location struct > > } ;

< NC Iu Mode Only Cell List struct > ::=
	{ 1 < List of added cells : < Add Iu Mode Only Cell List struct > >} ** 0;

< Add Iu Mode Only Cell List struct > ::=
	< START_FREQUENCY : bit (10) >
	< BSIC : bit (6) >
	{ 0 | 1 < Cell selection params : < Iu Mode Only Cell Selection struct > > }
			< NR_OF_FREQUENCIES : bit (5) >
			< FREQ_DIFF_LENGTH : bit (3) >
			{ < FREQUENCY_DIFF : bit (val(FREQ_DIFF_LENGTH)) >
			< BSIC : bit (6) >
			{ 0 | 1	< Cell selection params : 
					< Iu Mode Only Cell Selection struct > > } } * (val(NR_OF_FREQUENCIES)) ;

< Iu Mode Only Cell Selection struct > ::=
	< CELL BAR QUALIFY 3 : bit (2) >
	< SAME_RA_AS_SERVING_CELL : bit (1) >
	{ 0 | 1	< GPRS_RXLEV_ACCESS_MIN : bit (6) >
			< GPRS_MS_TXPWR_MAX_CCH : bit (5) > }
	{ 0 | 1	< GPRS_TEMPORARY_OFFSET : bit (3) >
			< GPRS_PENALTY_TIME : bit (5) > }
	{ 0 | 1	< GPRS_RESELECT_OFFSET : bit (5) > }
	{ 0 | 1	< HCS params : < HCS struct > > }
	{ 0 | 1	< SI13Alt_PBCCH_LOCATION : < SI13_PBCCH_LOCATION struct > > } ;

< GPRS 3G Additional Measurement Parameters Description 2 struct > ::= 
	{ 0 | 1 < FDD_REPORTING_THRESHOLD_2 : bit (6) > } ;								-- FDD Parameters

< Priority and E-UTRAN Parameters Description struct > ::=
	{ 0 | 1	< Serving Cell Priority Parameters Description : < GSM Priority Parameters IE >> }
	{ 0 | 1	< 3G Priority Parameters Description : < 3G Priority Parameters IE >> }
	{ 0 | 1	< E-UTRAN Parameters Description : < E-UTRAN Parameters IE >> } ;

< CSG Cells Reporting Description struct > ::=
	{ 0 | 1 < UTRAN CSG Cells Reporting Description : < UTRAN CSG Cells Reporting Description struct >> }
	{ 0 | 1 < E-UTRAN CSG Cells Reporting Description : <E-UTRAN CSG Cells Reporting Description struct >> } ;

< UTRAN CSG Cells Reporting Description struct > ::=
	{ 0 | 1	< UTRAN_CSG_FDD_REPORTING_THRESHOLD : bit (3) > 
			< UTRAN_CSG_FDD_REPORTING_THRESHOLD_2 : bit (6) > }
	{ 0 | 1	< UTRAN_CSG_TDD_REPORTING_THRESHOLD : bit (3) > } ;

< E-UTRAN CSG Cells Reporting Description struct > ::=
	{ 0 | 1	< E-UTRAN_CSG_FDD_REPORTING_THRESHOLD : bit (3) > 
			< E-UTRAN_CSG_FDD_REPORTING_THRESHOLD_2 : bit (6) > }
	{ 0 | 1	< E-UTRAN_CSG_TDD_REPORTING_THRESHOLD : bit (3) > 
			< E-UTRAN_CSG_TDD_REPORTING_THRESHOLD_2 : bit (6) > } ;

