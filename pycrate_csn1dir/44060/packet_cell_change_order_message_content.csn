-- TS 44.060 - d60
-- 11.2.4 Packet Cell Change Order
-- Packet Cell Change Order message content

< Packet Cell Change Order message content > ::=
	< PAGE_MODE : bit (2) >
	{	{ 0	< Global TFI : < Global TFI IE > >
		| 10	< TLLI / G-RNTI : bit (32) > }
		{ 0		
			{	< IMMEDIATE_REL : bit >
				< GSM target cell: < GSM target cell struct >>
				! < Non-distribution part error : bit (*) = < no string > > }
		| 1
			{ 00	-- Message escape
				{	< IMMEDIATE_REL : bit >
					{ 0 | 1 < UTRAN FDD Target cell: < UTRAN FDD Target cell IE > > }
					{ 0 | 1 < UTRAN TDD Target cell: < UTRAN TDD Target cell IE > > }
					{ null | 0	bit ** = < no string >	-- Receiver compatible with earlier release
					| 1		-- Additions in Rel-5 :
						{ 0 | 1 < G-RNTI extension : bit (4) > }
							{ null | 0	bit ** = < no string >	-- Receiver compatible with earlier release
							| 1		-- Additions in Rel-8
								{ 0 | 1	< E-UTRAN Target cell : < E-UTRAN Target cell IE >> }
								{ 0 | 1	< Individual Priorities : < Individual Priorities IE >> }	
									{ null | 0	bit ** = < no string >	-- Receiver compatible with earlier release
									| 1		-- Additions in Rel-11
										{ 0 | 1	< E-UTRAN Target cell with extended EARFCN :
 												< E-UTRAN Target cell with extended EARFCN IE >> }
										{ 0 | 1	< E-UTRAN IPP with extended EARFCNs:
 												< E-UTRAN IPP with extended EARFCNs IE >> }
						< padding bits > } } }
				! < Non-distribution part error : bit (*) = < no string > > }
			! < Message escape : { 01 | 10 | 11} bit (*) = <no string> > } }
		! < Address information part error : bit (*) = < no string > > }
	! < Distribution part error : bit (*) = < no string > > ;

< GSM target cell struct > ::=
	< ARFCN : bit (10) >
	< BSIC : bit (6) >
	< NC Measurement Parameters : < NC Measurement Parameters struct > >
	{ null | 0	bit ** = < no string >		-- Receiver compatible with earlier release
	| 1		-- Additions in release 98 :
		{ 0 | 1 < LSA Parameters : < LSA Parameters IE >> }
		{ null | 0	bit ** = < no string >		-- Receiver compatible with earlier release
		| 1		-- Additions in release 99 :
			< ENH Measurement parameters : < ENH Measurement parameters struct >> 
			{ null | 0	bit ** = < no string >		-- Receiver compatible with earlier release
			| 1		-- Additions in Rel-4 :
				< CCN_ACTIVE : bit (1) >
				{ 0 | 1	< CONTAINER_ID : bit (2) > }
				{ 0 | 1 < CCN Support Description : < CCN Support Description struct >> }
				{ null | 0	bit ** = < no string >		-- Receiver compatible with earlier release
				| 1		-- Additions in Rel-5 :
					{ 0 | 1	< G-RNTI extension : bit (4) > }
					{ 0 | 1	< Iu Mode Neighbour Cell Parameters : { 1 < Iu Mode Neighbour Cell params struct > } ** 0 > }												--Supplementary information for dual Iu mode and A/Gb mode capable cells
					{ 0 | 1	< NC Iu MODE ONLY CAPABLE CELL LIST : NC Iu Mode Only Cell List struct > }
					{ 0 | 1	< GPRS 3G Additional Measurement Parameters Description 2 : 
							< GPRS 3G Additional Measurement Parameters Description 2 struct >>}
					{ null | 0	bit ** = < no string >		-- Receiver compatible with earlier release
					| 1		-- Additions in Rel-6 :
						< 3G_CCN_ACTIVE : bit (1) >
						{ null | 0	bit ** = < no string >		-- Receiver compatible with earlier release
						| 1		-- Additions in Rel-7 :	
							{ 0 | 1	< 700_REPORTING_OFFSET : bit (3) > 
									< 700_REPORTING_THRESHOLD : bit (3) > }
							{ 0 | 1	< 810_REPORTING_OFFSET : bit (3) > 
									< 810_REPORTING_THRESHOLD : bit (3) > }
							{ null	| 0 bit** = <no string >	-- Receiver backward compatible with earlier version
									| 1								-- Additions for Rel-8
									< E-UTRAN_CCN_ACTIVE : bit (1) >
									{ 0 | 1 < Individual Priorities : < Individual Priorities IE >> }
							< padding bits > } } } } } } } ;

< NC Measurement Parameters struct > ::=
	< NETWORK_CONTROL_ORDER : bit (2) >
	{ 0 | 1 < NC_NON_DRX_PERIOD : bit (3) >
			< NC_REPORTING_PERIOD_I : bit (3) >
			< NC_REPORTING_PERIOD_T : bit (3) > }
	{ 0 | 1 < NC_FREQUENCY_LIST : NC Frequency list struct > } ;

< NC Frequency list struct > ::=
	{ 0 | 1	< NR_OF_REMOVED_FREQ : bit (5) >
			{ < REMOVED_FREQ_INDEX : bit (6) > } * (1 + val(NR_OF_REMOVED_FREQ)) }
	{ 1 < List of added Frequency : < Add Frequency list struct > >} ** 0;

< Add Frequency list struct > ::=
	< START_FREQUENCY : bit (10) >
	< BSIC : bit (6) >
	{ 0 | 1 < Cell selection params : < Cell Selection struct > > }
			< NR_OF_FREQUENCIES : bit (5) >
			< FREQ_DIFF_LENGTH : bit (3) >
			{ < FREQUENCY_DIFF : bit (val(FREQ_DIFF_LENGTH)) >
			< BSIC : bit (6) >
			{ 0 | 1	< Cell selection params : < Cell Selection struct > > } } * (val(NR_OF_FREQUENCIES)) ;

< Cell Selection struct > ::=
	< CELL_BAR_ACCESS_2 : bit (1) >
	< EXC_ACC : bit >
	< SAME_RA_AS_SERVING_CELL : bit (1) >
	{ 0 | 1	< GPRS_RXLEV_ACCESS_MIN : bit (6) >
			< GPRS_MS_TXPWR_MAX_CCH : bit (5) > }
	{ 0 | 1	< GPRS_TEMPORARY_OFFSET : bit (3) >
			< GPRS_PENALTY_TIME : bit (5) > }
	{ 0 | 1	< GPRS_RESELECT_OFFSET : bit (5) > }
	{ 0 | 1	< HCS params : < HCS struct > > }
	{ 0 | 1	< SI13_PBCCH_LOCATION : < SI13_PBCCH_LOCATION struct > > } ;

< SI13_PBCCH_LOCATION struct > ::=
	{ 0	< SI13_LOCATION : bit (1) >
	| 1	< PBCCH_LOCATION : bit (2) >
		< PSI1_REPEAT_PERIOD : bit (4) > } ;

< HCS struct > ::=
	< PRIORITY_CLASS : bit (3) >
	< HCS_THR : bit (5) > ;

< ENH Measurement parameters struct > ::= 
	{ 0 < BA_IND : bit > < 3G_BA_IND : bit > | 1 < PSI3_CHANGE_MARK : bit(2) > }
	< PMO_IND : bit >
	< REPORT_TYPE : bit >
	< REPORTING_RATE : bit >
	< INVALID_BSIC_REPORTING : bit >
	{ 0 | 1	< 3G Neighbour Cell Description : < 3G Neighbour Cell Description struct >> }
	{ 0 | 1	< GPRS REP PRIORITY Description : < GPRS REP PRIORITY Description struct >> }
	{ 0 | 1	< GPRS MEASUREMENT Parameters Description : 
			< GPRS MEASUREMENT PARAMETERS Description struct >> }
	{ 0 | 1	< GPRS 3G MEASUREMENT Parameters Description : 
			< GPRS 3G MEASUREMENT PARAMETERS Description struct >> } ;

< 3G Neighbour Cell Description struct > ::=
	{ 0 | 1 < Index_Start_3G : bit (7) > }
	{ 0 | 1 < Absolute_Index_Start_EMR : bit (7) > }
	{ 0 | 1 < UTRAN FDD Description : < UTRAN FDD Description struct >> }
	{ 0 | 1 < UTRAN TDD Description : < UTRAN TDD Description struct >> }
	{ 0 | 1 < REMOVED_3GCELL_Description : < REMOVED_3GCELL_Description struct >> } ; 

< REMOVED_3GCELL_Description struct > ::=
	< N1 : bit (2) >
	{	< N2 : bit (5) >
		{	< REMOVED_3GCELL_INDEX : bit (7) >
			< 3G_CELL_DIFF_LENGTH : bit (3) >
			< 3GCELL_DIFF : bit (val(3G_CELL_DIFF_LENGTH)) >
		} * (1+val(N2))
	} * (1+val(N1)) ;

< UTRAN FDD Description struct > ::=
	{ 0 | 1 < Bandwidth_FDD : bit (3) > }
	{ 1 < Repeated UTRAN FDD Neighbour Cells : < Repeated UTRAN FDD Neighbour Cells struct >> } ** 0 ;

< Repeated UTRAN FDD Neighbour Cells struct > ::=
	0 < FDD-ARFCN : bit (14) >			-- The value ‘1’ was used in an earlier 
											-- version of the protocol and shall not be used.
	< FDD_Indic0 : bit >
	< NR_OF_FDD_CELLS : bit (5) >
	< FDD_CELL_INFORMATION Field : bit(p(NR_OF_FDD_CELLS)) > ; 

< UTRAN TDD Description struct > ::=
	{ 0 | 1 < Bandwidth_TDD : bit (3) > }
	{ 1 < Repeated UTRAN TDD Neighbour Cells : < Repeated UTRAN TDD Neighbour Cells struct >> } ** 0 ;

< Repeated UTRAN TDD Neighbour Cells struct > ::=
	0 < TDD-ARFCN : bit (14) >			-- The value ‘1’ was used in an earlier
											-- version of the protocol and shall not be used.
	< TDD_Indic0 : bit >
	< NR_OF_TDD_CELLS : bit (5) >
	< TDD_CELL_INFORMATION Field : bit(q(NR_OF_TDD_CELLS)) > ; 

< GPRS REP PRIORITY Description struct > ::=
	< Number_Cells : bit(7) >
	{ < REP_PRIORITY : bit > } * (val(Number_Cells)) ;

< GPRS MEASUREMENT PARAMETERS Description struct > ::=
	{ 0 | 1	< MULTIBAND_REPORTING : bit (2) > }
	{ 0 | 1	< SERVING_BAND_REPORTING : bit (2) > }
	< SCALE_ORD : bit(2) >

	{ 0 | 1	< 900_REPORTING_OFFSET : bit (3) > 
			< 900_REPORTING_THRESHOLD : bit (3) > }

	{ 0 | 1	< 1800_REPORTING_OFFSET : bit (3) > 
			< 1800_REPORTING_THRESHOLD : bit (3) > }

	{ 0 | 1	< 400_REPORTING_OFFSET : bit (3) > 
			< 400_REPORTING_THRESHOLD : bit (3) > }

	{ 0 | 1	< 1900_REPORTING_OFFSET : bit (3) > 
			< 1900_REPORTING_THRESHOLD : bit (3) > }

	{ 0 | 1	< 850_REPORTING_OFFSET : bit (3) > 
			< 850_REPORTING_THRESHOLD : bit (3) > } ;

< GPRS 3G MEASUREMENT PARAMETERS Description struct > ::=
	< Qsearch_P : bit (4) > 
	{ 1 ! < Ignore : bit = < no string >> }			-- this bit shall be ignored by the receiver
													-- for backward compatibility with earlier releases
	{ 0 | 1 < FDD_REP_QUANT : bit >													-- FDD Parameters
			< FDD_MULTIRAT_REPORTING : bit (2) > }

	{ 0 | 1	< FDD_REPORTING_OFFSET : bit (3) > 
			< FDD_REPORTING_THRESHOLD : bit (3) > }

	{ 0 | 1	< TDD_MULTIRAT_REPORTING : bit (2) > }								-- TDD Parameters
	{ 0 | 1	< TDD_REPORTING_OFFSET : bit (3) > 
			< TDD_REPORTING_THRESHOLD : bit (3) > } ;

< CCN Support Description struct > ::=
	< Number_Cells : bit (7) >
	{ < CCN_SUPPORTED : bit > } * (val(Number_Cells)) ;

< Iu Mode Neighbour Cell Params struct > ::=
	{ 0 | 1 < Iu Mode Cell Selection Params : <Iu Mode Cell Selection struct > > }
			< NR_OF_FREQUENCIES : bit (5) >
			{ 0 | 1	< Iu Mode Cell Selection Params : 
					< Iu Mode Cell Selection struct > > } * (val(NR_OF_FREQUENCIES)) ;

< Iu Mode Cell Selection struct > ::=
	< CELL BAR QUALIFY 3 : bit (2) >	
	{ 0 | 1	< SI13Alt PBCCH Location: < SI13 PBCCH Location struct > > } ;

< NC Iu Mode Only Cell List struct > ::=
	{ 1 < List of added cells : < Add Iu Mode Only Cell List struct > >} ** 0;

< Add Iu Mode Only Cell List struct > ::=
	< START_FREQUENCY : bit (10) >
	< BSIC : bit (6) >
	{ 0 | 1	< Cell selection params : < Iu Mode Only Cell Selection struct > > }
			< NR_OF_FREQUENCIES : bit (5) >
			< FREQ_DIFF_LENGTH : bit (3) >
			{ < FREQUENCY_DIFF : bit (val(FREQ_DIFF_LENGTH)) >
			< BSIC : bit (6) >
			{ 0 | 1	< Cell selection params : < Iu Mode Only Cell Selection struct > > } } * (val(NR_OF_FREQUENCIES)) ;

< Iu Mode Only Cell Selection struct > ::=
	< CELL BAR QUALIFY 3 : bit (2) >
	< SAME_RA_AS_SERVING_CELL : bit (1) >
	{ 0 | 1	< GPRS_RXLEV_ACCESS_MIN : bit (6) >
			< GPRS_MS_TXPWR_MAX_CCH : bit (5) > }
	{ 0 | 1	< GPRS_TEMPORARY_OFFSET : bit (3) >
			< GPRS_PENALTY_TIME : bit (5) > }
	{ 0 | 1	< GPRS_RESELECT_OFFSET : bit (5) > }
	{ 0 | 1	< HCS params : < HCS struct > > }
	{ 0 | 1	< SI13Alt_PBCCH_LOCATION : < SI13_PBCCH_LOCATION struct > > } ;

< GPRS 3G Additional Measurement Parameters Description 2 struct > ::= 
	{ 0 | 1 < FDD_REPORTING_THRESHOLD_2 : bit (6) > } ;							-- FDD Parameters

